/*
 * Copyright ConsenSys AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'java-library'

jar {
  baseName 'besu-ethereum-rlp'
  manifest {
    attributes(
      'Specification-Title': baseName,
      'Specification-Version': project.version,
      'Implementation-Title': baseName,
      'Implementation-Version': calculateVersion()
      )
  }
}

dependencies {
  api project(':plugin-api')
  api project(':util')

  implementation 'com.google.guava:guava'
  implementation 'io.vertx:vertx-core'

  compileOnly 'org.openjdk.jmh:jmh-generator-annprocess'

  jmh project(':util')

  testImplementation project(path:':ethereum:referencetests', configuration: 'testOutput')
  testImplementation project(':testutil')

  testImplementation 'org.assertj:assertj-core'
  testImplementation 'com.fasterxml.jackson.core:jackson-databind'
  testImplementation 'junit:junit'
}

test {
  exclude 'org/hyperledger/besu/ethereum/rlp/InvalidRLPRefTest.class'
  exclude 'org/hyperledger/besu/ethereum/rlp/RLPRefTest.class'
}

task referenceTests(type: Test, dependsOn: ['compileTestJava']) {
  doFirst {
    if (!file("../referencetests/src/test/resources/README.md").exists()) {
      throw new GradleException("ethereum/referencetests/src/test/resources/README.md missing: please clone submodules (git submodule update --init --recursive)")
    }
  }
  include 'org/hyperledger/besu/ethereum/rlp/InvalidRLPRefTest.class'
  include 'org/hyperledger/besu/ethereum/rlp/RLPRefTest.class'
}
