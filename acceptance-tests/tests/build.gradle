/*
 * Copyright 2019 ConsenSys AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */

plugins {
  id 'org.web3j' version '5.0.2'
  id 'org.web3j.solidity' version '0.6.1'
}

web3j { generatedPackageName = 'org.hyperledger.besu.tests.web3j.generated' }

sourceSets.main.solidity.srcDirs = ["$projectDir/contracts"]

solidity {
  resolvePackages = false
  // TODO: remove the forced version, when DEV network is upgraded to support latest forks
  version = '0.8.19'
  evmVersion = 'london'
}

testing {
  suites {
    acceptanceTest(JvmTestSuite) {
      useJUnitJupiter()
      dependencies {
        annotationProcessor project()
        implementation project()

        implementation project(':acceptance-tests:dsl')
        implementation project(':acceptance-tests:tests:shanghai')
        implementation project(':config')
        implementation project(':crypto:algorithms')
        implementation project(':datatypes')
        implementation project(':ethereum:api')
        implementation project(':ethereum:core')
        implementation project(':ethereum:eth')
        implementation project(':ethereum:permissioning')
        implementation project(':ethereum:p2p')
        implementation project(':metrics:core')
        implementation project(':plugins:rocksdb')
        implementation project(':plugin-api')

        implementation files("${rootProject.projectDir}/acceptance-tests/detached-test-plugins/build/libs/testPlugins.jar")

        implementation 'commons-io:commons-io'
        implementation 'com.google.guava:guava'
        implementation 'com.google.protobuf:protobuf-java'
        implementation 'io.grpc:grpc-all'
        implementation 'io.opentelemetry:opentelemetry-extension-trace-propagators'
        implementation 'io.opentelemetry:opentelemetry-sdk'
        implementation 'io.opentelemetry.instrumentation:opentelemetry-okhttp-3.0'
        implementation 'io.opentelemetry.proto:opentelemetry-proto'
        implementation 'jakarta.validation:jakarta.validation-api'
        implementation 'org.apache.commons:commons-compress'
        implementation 'org.assertj:assertj-core'
        implementation 'org.awaitility:awaitility'
        implementation 'org.junit.jupiter:junit-jupiter'
        implementation 'org.web3j:abi'
        implementation 'org.web3j:core'
      }

      sourceSets {
        acceptanceTest {
          resources {
            srcDirs "${rootDir}/acceptance-tests/detached-test-plugins/build/distributions"
            srcDirs "${rootDir}/acceptance-tests/detached-test-plugins/dependency-conflict-1/build/distributions"
            srcDirs "${rootDir}/acceptance-tests/detached-test-plugins/dependency-conflict-2/build/distributions"
            srcDirs "${rootDir}/acceptance-tests/detached-outdated-test-plugins/build/distributions"
            srcDirs "${rootDir}/acceptance-tests/catalogless-test-plugins/build/libs"
          }
        }
      }

      targets {
        all {
          testTask.configure {
            inputs.property "integration.date", LocalTime.now() // so it runs at every invocation

            exclude '**/bftsoak/**'

            dependsOn(rootProject.installDist)
            setSystemProperties(getSystemProperties())
            systemProperty 'acctests.runBesuAsProcess', 'true'
            systemProperty 'java.security.properties', "${buildDir}/resources/acceptanceTest/acceptanceTesting.security"
            def javaProjects = rootProject.subprojects - project(':platform')
            mustRunAfter javaProjects.test
            description = 'Runs ALL Besu acceptance tests (excluding bftsoak).'
            group = 'verification'

            jvmArgs "-XX:ErrorFile=${buildDir}/jvmErrorLogs/java_err_pid%p.log"

            testLogging {
              exceptionFormat = 'full'
              showStackTraces = true
              showStandardStreams = Boolean.getBoolean('acctests.showStandardStreams')
              showExceptions = true
              showCauses = true
            }

            doFirst { mkdir "${buildDir}/jvmErrorLogs" }
          }
        }

        acceptanceTestBftSoak {
          testTask.configure {
            include '**/bftsoak/**'

            // Set to any time > 60 minutes to run the soak test for longer
            // systemProperty 'acctests.soakTimeMins', '120'
            description = 'Runs BFT soak test.'

            testLogging {
              showStandardStreams = true
            }
          }
        }
      }
    }
  }
}

// Add testSupportArtifacts dependency to acceptanceTest suite
dependencies {
  acceptanceTestImplementation rootProject.project(':ethereum:core').configurations.named('testSupportArtifacts').get().allArtifacts.files
}

processAcceptanceTestResources.dependsOn(
  ':buildDetachedTestPlugins',
  ':buildDetachedOutdatedTestPlugins',
  ':acceptance-tests:catalogless-test-plugins:jar'
  )

compileAcceptanceTestJava.dependsOn ':buildDetachedTestPlugins'
