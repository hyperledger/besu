/*
 * Copyright contributors to Hyperledger Besu.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'application'
apply plugin: 'java-library'
apply plugin: 'jacoco'

jar {
  archiveBaseName = calculateArtifactId(project)
  manifest {
    attributes(
      'Specification-Title': archiveBaseName,
      'Specification-Version': project.version,
      'Implementation-Title': archiveBaseName,
      'Implementation-Version': calculateVersion(),
      'Commit-Hash': getGitCommitDetails(40).hash
      )
  }
}

dependencies {
  implementation project(':app')
  implementation project(':crypto:algorithms')
  implementation project(':datatypes')
  implementation project(':ethereum:referencetests')
  implementation project(':evm')
  implementation project(':util')

  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.google.guava:guava'
  implementation 'info.picocli:picocli'
  implementation 'io.consensys.tuweni:tuweni-bytes'
  implementation 'org.jacoco:org.jacoco.agent'
  implementation 'org.jacoco:org.jacoco.core'
  implementation 'org.mockito:mockito-core'
}

application {
  applicationName = 'BesuFuzz'
  mainClass = 'org.hyperledger.besu.testfuzz.BesuFuzz'
  applicationDefaultJvmArgs = [
    '-javaagent:$APP_HOME/lib/jacocoagent.jar'
  ]
}

// P256 signature verification fuzzer tasks
def p256CorpusDir = "${buildDir}/generated/p256-corpus"

tasks.register("runP256Fuzzer", JavaExec) {
  doNotTrackState("Produces no artifacts")
  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'org.hyperledger.besu.testfuzz.BesuFuzz'

  args = [
    "p256verify",
    "--corpus-dir=${p256CorpusDir}"
  ]

  doFirst {
    mkdir p256CorpusDir
  }
}

// Standard P256 fuzzing task
tasks.register("fuzzP256Verify") {
  dependsOn "generateP256Corpus"
  doLast {
    runP256Fuzzer.args += "--guidance-regexp=org/(hyperledger/besu|apache/tuweni)"
    runP256Fuzzer.args += "--new-corpus-dir=${p256CorpusDir}/new"
  }
  finalizedBy("runP256Fuzzer")
}

// Task to generate initial P256 corpus
tasks.register("generateP256Corpus", JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'org.hyperledger.besu.testfuzz.P256VerifyCorpusGenerator'
  args = [p256CorpusDir]

  doFirst {
    mkdir p256CorpusDir
  }
}

jacoco {
  applyTo run
  applyTo runP256Fuzzer
}

// Copies jacoco into the lib directory
tasks.register("copyJacoco", Copy) {
  // The jacocoagent.jar is embedded within the jar
  from zipTree(configurations.jacocoAgent.singleFile).filter { it.name == 'jacocoagent.jar' }.singleFile
  into layout.buildDirectory.dir("install/${application.applicationName}/lib")
}

installDist.finalizedBy copyJacoco

startScripts {
  defaultJvmOpts = [
    "-Dsecp256k1.randomize=false"
  ]
  unixStartScriptGenerator.template = resources.text.fromFile("${projectDir}/src/main/scripts/unixStartScript.txt")
  windowsStartScriptGenerator.template = resources.text.fromFile("${projectDir}/src/main/scripts/windowsStartScript.txt")
  doLast { tweakStartScript(startScripts) }
}

static def tweakStartScript(createScriptTask) {
  def shortenWindowsClasspath = { line ->
    line.replaceAll(/^set CLASSPATH=.*$/, "set CLASSPATH=%APP_HOME%/lib/*")
  }

  createScriptTask.unixScript.text = createScriptTask.unixScript.text.replace('BESU_HOME', '\$APP_HOME')
  createScriptTask.windowsScript.text = createScriptTask.windowsScript.text.replace('BESU_HOME', '%~dp0..')

  // Prevent the error originating from the 8191 chars limit on Windows
  createScriptTask.windowsScript.text =
    createScriptTask.windowsScript
    .readLines()
    .collect(shortenWindowsClasspath)
    .join('\r\n')
}
